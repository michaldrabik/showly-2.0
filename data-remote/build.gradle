apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: 'dagger.hilt.android.plugin'
apply from: '../versions.gradle'

android {
  kotlinOptions { jvmTarget = "1.8" }
  compileOptions {
    coreLibraryDesugaringEnabled true
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  
  compileSdkVersion versions.compileSdk

  buildFeatures {
    buildConfig = true
  }

  defaultConfig {
    minSdkVersion versions.minSdk
    targetSdkVersion versions.compileSdk

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
  }

  buildTypes.all {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    buildConfigField("String", "TRAKT_CLIENT_ID", properties.getProperty("traktClientId"))
    buildConfigField("String", "TRAKT_CLIENT_SECRET", properties.getProperty("traktClientSecret"))
    buildConfigField("String", "TMDB_API_KEY", properties.getProperty("tmdbApiKey"))
    buildConfigField("String", "OMDB_API_KEY", properties.getProperty("omdbApiKey"))
    buildConfigField("String", "REDDIT_CLIENT_ID", properties.getProperty("redditClientId"))
    buildConfigField 'String', 'VER_NAME', "\"${versions.versionName}\""
  }

  buildTypes {
    debug {
      minifyEnabled false
    }

    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
    }
  }
}

dependencies {
  api "com.squareup.retrofit2:retrofit:$versions.retrofit"
  api "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
  api "com.squareup.okhttp3:logging-interceptor:$versions.okHttpLogInterceptor"

  implementation "com.jakewharton.timber:timber:$versions.timber"

  implementation "com.google.dagger:hilt-android:$versions.hilt"
  kapt "com.google.dagger:hilt-compiler:$versions.hilt"

  implementation "com.google.firebase:firebase-crashlytics:$versions.firebaseCrashlytics"

  testImplementation "junit:junit:$versions.junit"

  coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}
